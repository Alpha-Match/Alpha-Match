plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.5'
}

group = 'com.alpha'
version = '0.0.1-SNAPSHOT'
description = 'batch'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springGrpcVersion', "1.0.0")
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-batch-jdbc'  // JDBC mode (Spring Boot 4.0 변경사항)
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Spring Batch (명시적으로 compile 의존성 추가 - Spring Boot 4.0.0 issue)
	implementation 'org.springframework.batch:spring-batch-core'
	implementation 'org.springframework.batch:spring-batch-infrastructure'

	// gRPC
	implementation 'io.grpc:grpc-services'
	implementation 'io.grpc:grpc-stub'
	implementation 'io.grpc:grpc-protobuf'
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'

	// Flyway (DB Migration)
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'

	// PostgreSQL + pgvector
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'com.pgvector:pgvector:0.1.4'

	// Reactor
	implementation 'io.projectreactor:reactor-core'
	implementation 'io.projectreactor.addons:reactor-extra'

	// Jackson Datatype Modules (for Java 8 Time API support)
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-batch-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-quartz-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
	testImplementation 'org.springframework.grpc:spring-grpc-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
	}
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				option '@generated=omit'
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
